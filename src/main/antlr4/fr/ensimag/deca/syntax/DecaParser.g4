parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import fr.ensimag.deca.tools.*;
    import java.io.PrintStream;
    import java.math.BigDecimal;
}

@members {
    @Override
    protected AbstractProgram parseProgram() { 
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]

    : i=ident {
            AbstractInitialization init = new NoInitialization();
            $tree = new DeclVar($t, $i.tree, init);
            setLocation($tree, $i.start);
            setLocation(init, $i.start);
        }
      (EQUALS e=expr {
            AbstractInitialization init2 = new Initialization($e.tree);
            $tree = new DeclVar($t, $i.tree, init2);
            setLocation(init2, $e.start);
        }
      )? {
            setLocation($tree, $i.start);
        }
    ;

list_inst returns[ListInst tree]
@init {
    $tree = new ListInst();
} 
    : (i=inst {
        $tree.add($i.tree);
        }
      )*
    ;

inst returns[AbstractInst tree]
    :

    e=expr SEMI{
        assert($e.tree != null);
        $tree=$e.tree;
    }
    | SEMI {

    }

    | PRINT OPARENT e3=list_expr CPARENT SEMI {
        $tree = new Print(false,$e3.tree);
        setLocation($tree, $PRINT);
    }
    | PRINTLN OPARENT e4=list_expr CPARENT SEMI {
        $tree = new Println(false,$e4.tree);
        setLocation($tree, $PRINTLN);
    }
    | PRINTX OPARENT e3=list_expr CPARENT SEMI {
        $tree = new Print(true,$e3.tree);
        setLocation($tree, $PRINTX);
    }
    | PRINTLNX OPARENT e4=list_expr CPARENT SEMI {
        $tree = new Println(true,$e4.tree);
        setLocation($tree, $PRINTLNX);
    }
    | WHILE OPARENT e5=expr CPARENT OBRACE e6=list_inst CBRACE {
        $tree = new While($e5.tree, $e6.tree);
        setLocation($tree, $WHILE);
    }
    | e7=if_then_else{
        $tree = $e7.tree;
    }
    | RETURN e8=expr SEMI{
        assert($e8.tree != null);
        $tree = new Return($e8.tree);
        setLocation($tree, $RETURN);
 

    }
    ;


if_then_else returns[IfThenElse tree]
@init {
    ListInst elseB;
    ListInst auxB;
    List<ListInst> else_list;
}
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
        elseB = new ListInst(); 
        $tree = new IfThenElse($condition.tree, $li_if.tree, elseB);
        setLocation($tree, $if1);
      }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {

        ListInst else_auxB = new ListInst();
        IfThenElse newIf = new IfThenElse($elsif_cond.tree, $elsif_li.tree, else_auxB);
        elseB.add(newIf);
        elseB = else_auxB;
        setLocation(newIf, $elsif);
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
        for(AbstractInst i: $li_else.tree.getList()){
            elseB.add(i);
        }
      }
      )?
    ;

list_expr returns[ListExpr tree]
@init   {
            $tree = new ListExpr();
        }
    : (e1=expr {
            $tree.add($e1.tree);
        }
       (COMMA e2=expr {
            $tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : e=assign_expr{
        $tree = $e.tree;
    };
assign_expr returns[AbstractExpr tree]
    : e=or_expr ( 
        /* condition: expression e must be a "LVALUE" */ {

            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
            
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);

            $tree = new Assign((AbstractLValue) $e.tree,$e2.tree);
            setLocation($tree, $EQUALS);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new Or($e1.tree,$e2.tree);
            setLocation($tree, $e1.start);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);

            $tree = new And($e1.tree,$e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new Equals($e1.tree,$e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new NotEquals($e1.tree,$e2.tree);
            setLocation($tree,$e1.start);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree=$e.tree;
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new LowerOrEqual($e1.tree,$e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new GreaterOrEqual($e1.tree,$e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new Lower($e1.tree,$e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr INSTANCEOF e3=type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($e1.tree, $e3.tree);
            setLocation($tree, $e1.start);

        }
    ;

sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);

            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);

            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);

            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);

            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $MINUS);
        }
    | EXCLAM e1=unary_expr {
            assert($e1.tree != null);
            $tree = new Not($e1.tree);
            setLocation($tree, $EXCLAM);
        }
    | e2=select_expr {
            assert($e2.tree != null);
            $tree=$e2.tree;
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree=$e.tree;
        }
    | e1=select_expr DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
             $tree = new CallMethod($e1.tree,$i.tree, $args.tree);
             setLocation($tree, $DOT);
        }
        | /* epsilon */ {
            // we matched "e.i"
            $tree = new Dot($e1.tree, $i.tree);
            setLocation($tree, $DOT);
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            This classThis = new This();
            setLocation(classThis, $m.start);
            $tree = new CallMethod(new This(),$m.tree, $args.tree);
            setLocation($tree, $m.start);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
        }
    | READINT OPARENT CPARENT {
        $tree = new ReadInt();
        setLocation($tree, $READINT);
    }
    | READFLOAT OPARENT CPARENT {
        $tree = new ReadFloat();
        setLocation($tree, $READFLOAT);
        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $NEW);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($type.tree, $expr.tree);
            setLocation($tree, $cast);
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
        }
    ;

literal returns[AbstractExpr tree]
   : e=INT {

        try {
            long val = Long.parseLong($e.text);
            if (val > 0){
                val = Math.max( Integer.MIN_VALUE, val);
                val = Math.min( Integer.MAX_VALUE ,val );
            } else if (val < 0){
                val = Math.min( -Integer.MIN_VALUE, val);
                val = Math.max( -Integer.MAX_VALUE ,val );
            }
            $tree = new IntLiteral((int) val);
        } catch (NumberFormatException e) {
            throw new NumberFormatException("Your int is too long"); 
        }
        setLocation($tree, $e);
    }
    | e1=FLOAT {
        double val = Double.parseDouble($e1.text);

        if (val > 0){
            val = Math.max( Float.MIN_VALUE, val);
            val = Math.min( Float.MAX_VALUE ,val );
        } else if (val < 0){
            val = Math.min( -Float.MIN_VALUE, val);
            val = Math.max( -Float.MAX_VALUE ,val );
        }

        $tree = new FloatLiteral((float) val);
        setLocation($tree, $e1);
    }
    | e2=STRING{
        $tree = new StringLiteral($e2.text);
        setLocation($tree, $e2);
    }
    | TRUE {
        $tree = new BooleanLiteral(true);
        setLocation($tree, $TRUE);

        }
    | FALSE {
        $tree = new BooleanLiteral(false);
        setLocation($tree, $FALSE);
        }
    | THIS {
        $tree = new This();
        setLocation($tree, $THIS);
        }
    | NULL {
        $tree = new Null();
        setLocation($tree, $NULL);
        }

    ;

ident returns[AbstractIdentifier tree]
    : e=IDENT {
        $tree = new Identifier( getDecacCompiler().createSymbol($e.text));
        setLocation($tree, $e);
    }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init {
    $tree = new ListDeclClass();
} :
      (c1=class_decl {
        $tree.add($c1.tree);
        }
      )*
    ;

class_decl returns[DeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {

            $tree = new DeclClass($name.tree, $superclass.tree, $class_body.fields, $class_body.methods);
            setLocation($tree, $CLASS);
        }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS i=ident {
            $tree = new Identifier(getDecacCompiler().createSymbol($i.text));
            setLocation($tree, $EXTENDS);
        }
    | /* epsilon */ {
            $tree = new Identifier(getDecacCompiler().createSymbol("Object"));
            $tree.setLocation(Location.BUILTIN);
        }
    ;

class_body returns[ListDeclField fields, ListDeclMethod methods]
@init{
            $fields = new ListDeclField();
            $methods = new ListDeclMethod();
}
    : (m=decl_method {
            setLocation($m.tree, $m.start);
            $methods.add($m.tree);
        }
      | f=decl_field_set[$fields]
      )*
    ;

decl_field_set[ListDeclField fields] returns[ListDeclField tree]
    : v=visibility t=type ldf = list_decl_field[$fields, $v.vis, $type.tree]
      SEMI
    ;

visibility returns[Visibility vis]
    : /* epsilon */ {
        $vis = Visibility.PUBLIC;
        }
    | PROTECTED {
        $vis = Visibility.PROTECTED;
        }
    ;

list_decl_field [ListDeclField fields, Visibility v, AbstractIdentifier t]
    : dv1=decl_field [$v, $t] {
            $fields.add($dv1.tree);
                            }
        (COMMA dv2=decl_field[$v, $t] {
           $fields.add($dv2.tree);
            }
      )*
    ;

decl_field[Visibility v, AbstractIdentifier t] returns[AbstractDeclField tree]
    : i=ident {
            AbstractInitialization init = new NoInitialization();
            
         //   setLocation(init, $i.start);
        }
      (EQUALS e=expr {
            init = new Initialization($e.tree);
            $tree = new DeclField($v, $t, $i.tree, init);
            setLocation(init, $i.start);
        }
      )? {
            $tree = new DeclField($v, $t, $i.tree, init);
            setLocation($tree, $i.start);
        }
    ;

decl_method returns[AbstractDeclMethod tree]
    : t=type i=ident OPARENT params=list_params CPARENT 
    (b=block {
            assert($type.tree != null);
            assert($ident.tree != null);
            assert($params.tree != null);
            assert($block.decls != null);
            assert($block.insts != null);
            
            AbstractMethodBody body= new MethodBody($b.decls, $b.insts);
            setLocation(body, $block.start);

            $tree = new DeclMethod($type.tree, $ident.tree, $params.tree, body);
            setLocation($tree, $type.start); 
        }
    |  e1=ASM OPARENT s=multi_line_string CPARENT SEMI {
            assert($type.tree != null);
            assert($ident.tree != null);
            assert($params.tree != null);
            assert($s.text != null);

            $tree = new DeclMethodAsm($type.tree, $ident.tree, $params.tree,$s.text);
            setLocation($tree, $e1);
    })
    ;

list_params returns[ListDeclParam tree]
@init{
        $tree = new ListDeclParam();
}
    : (p1=param {
        $tree.add($p1.tree);

        } (COMMA p2=param {
        $tree.add($p2.tree);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    
    : s=MULTI_LINE_STRING {
            
            $text = $s.text;
            $location = tokenLocation($s);
        }
     |   s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns[DeclParam tree]
    : type ident {
            $tree = new DeclParam($type.tree, $ident.tree);
            setLocation($tree, $ident.start);
        }
    ;
